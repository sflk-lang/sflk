
# Indentation level #
lvl! < 0

# Is the current line indented yet #
indented! < 0

# Perform the indentation #
indent! < {
	x! < 0 lp wh x -lvl bd pr "| " bd x < x +1
}

interceptor! < {
	cy v
	handled! < 0
	# Intercepted signals #
	if name -.newline el dh {
		nl
		indented < 0
		handled < 1
	}
	if name -.print el dh {
		if indented el dh indent
		pr value
		indented < 1
		handled < 1
	}
	# Custom signals handling #
	if name -.indent el dh {
		lvl < lvl +1
		handled < 1
	}
	if name -.unindent el dh {
		lvl < lvl -1
		handled < 1
	}
	# Let the rest pass through #
	if handled el em v rs v
}

demo! < {
	# Custom signals emitters #
	indent! < {em ()>{name! < .indent v < cx}}
	unindent! < {em ()>{name! < .unindent v < cx}}

	# Test #
	pr "lvl 0" nl
	dh indent
		pr "lvl 1" nl
		dh indent
			pr "lvl 2" nl
			pr "lvl 2" nl
		dh unindent
		pr "lvl 1" nl
		dh indent
			dh indent
				pr "lvl 3" nl
				dh indent
					pr "lvl 4" nl
				dh unindent
			dh unindent
		dh unindent
		pr "lvl 1" nl
	dh unindent
	pr "lvl 0" nl
}

do demo wi interceptor
